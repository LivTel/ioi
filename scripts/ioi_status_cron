#!/bin/csh
# designed to be used from a cron job
# output in output_file
# $HeadURL$
# $Revision: 1.1 $
if ( ${?LT_HOME} == 0 ) then
    setenv LT_HOME /icc
endif
if ( ${?LT_BIN_HOME} == 0 ) then
    setenv LT_BIN_HOME ${LT_HOME}/bin
endif
if ( ${?HOSTTYPE} == 0 ) then
    setenv HOSTTYPE i386-linux
endif
if ( ${?LT_LIB_HOME} == 0 ) then
    setenv LT_LIB_HOME ${LT_BIN_HOME}/lib/${HOSTTYPE}
endif
if ( ${?LD_LIBRARY_PATH} ) then
   setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}":"$LT_LIB_HOME
else
   setenv LD_LIBRARY_PATH $LT_LIB_HOME
endif
set classpath_string = ".:${LT_BIN_HOME}/javalib/ngat_message_base.jar:${LT_BIN_HOME}/javalib/ngat_message_inst_dp.jar:${LT_BIN_HOME}/javalib/ngat_message_iss_inst.jar:${LT_BIN_HOME}/javalib/ngat_net.jar:${LT_BIN_HOME}/javalib/ngat_phase2.jar:${LT_BIN_HOME}/javalib/ngat_util.jar:${LT_BIN_HOME}/javalib/ngat_util_logging.jar:${LT_BIN_HOME}/javalib/ngat_io_i.jar"
if ( ${?CLASSPATH} == 0 ) then
   setenv CLASSPATH ${classpath_string}
endif
if ( ${?HOST} == 0 ) then
    setenv HOST ioilinux
endif
set host = ioilinux
set port_number = 7373
set tmp_file = /tmp/ioi_status_cron.txt
set tmp_err_file = /tmp/ioi_status_cron_err.txt
set date_doy_string = `date +"%j"`
set output_file = ${LT_HOME}/log/ioi_status_cron_${date_doy_string}.csv
(java ngat.ioi.test.SendGetStatusCommand -ip ${host} -port ${port_number} -level 1 > ${tmp_file}) >& ${tmp_err_file}
set date_string = `date`
set date2_string = `date +"%G%m%d%H%M%S"`
set date3_string = `date +"%s"`
set dewar_temperature = `cat ${tmp_file} | grep '^Temperature = ' | sed "s/.*Temperature = \(.*\)/\1/g"`
set dewar_temperature_0 = `cat ${tmp_file} | grep 'Temperature.0 = ' | sed "s/.*Temperature.0 = \(.*\)/\1/g"`
set dewar_temperature_1 = `cat ${tmp_file} | grep 'Temperature.1 = ' | sed "s/.*Temperature.1 = \(.*\)/\1/g"`
set heater_pcent = `cat ${tmp_file} | grep 'Heater PCent' | sed "s/Heater PCent = \(.*\)/\1/g"`
set heater_status = `cat ${tmp_file} | grep 'Heater Status' | grep -v 'Heater Status String' | sed "s/Heater Status = \(.*\)/\1/g"`
set heater_status_string = `cat ${tmp_file} | grep 'Heater Status String' | sed "s/Heater Status String = \(.*\)/\1/g"`
set ramp_ison_1 = `cat ${tmp_file} | grep 'Temperature.Ramp.Is_On.1' | sed "s/Temperature.Ramp.Is_On.1 = \(.*\)/\1/g"`
set ramp_rate_1 = `cat ${tmp_file} | grep 'Temperature.Ramp.Rate.1' | sed "s/Temperature.Ramp.Rate.1 = \(.*\)/\1/g"`
if ( ! -r ${output_file} ) then
    echo "Date,Date,Date,Dewar Temp,Dewar Temp0,Dewar Temp1,Heater PCent,Heater Status,Heater Status String,Ramp IsOn 1,Ramp Rate 1" > ${output_file}
endif
echo "${date_string},${date2_string},${date3_string},${dewar_temperature},${dewar_temperature_0},${dewar_temperature_1},${heater_pcent},${heater_status},${heater_status_string},${ramp_ison_1},${ramp_rate_1}" >> ${output_file}
